void automation.Calculate_Exchange_Rate(String DEAL_ID)
{
exchangeRates = invokeUrl
[
	url: "https://openexchangerates.org/api/latest.json?app_id=" + "APP_ID_HERE"
	type: GET
];
euroRate = ifNull(exchangeRates.get("rates"),{"EUR":0.85}).get("EUR");
successCount = 0;
totalDeals = 0;
deals = Map();
dealUpdates = list();
dealUpdates.add(list());
//
selectText = "select id, Pipeline, Amount from Deals where Pipeline = 'Europe' limit 500";
query = Map();
query.put("select_query",selectText);
searchResponse = invokeUrl
[
	url: "https://www.zohoapis.com/crm/v8/coql"
	type: POST
	parameters: queryMap.toString()
	connection: "AUTH_CONNECTION_HERE"
];
for each deal in searchResponse.get("data")
{
	deals.put(deal.get("id"),deal);
	totalDeals = totalDeals + 1;
}
for each dealId in deals.keys()
{
	dealDetails = deals.get(dealId);
	dealAmount = ifNull(dealDetails.get("Amount"),0);
	convertedAmount = dealAmount * euroRate;
	//
	thisDeal = Map();
	thisDeal.put("id",dealId);
	thisDeal.put("Amount_Euro",convertedAmount.truncate(2));
	thisDeal.put("Last_Amount_Calculation_Time",now.toString("YYYY-MM-dd'T'HH:mm:ss"));
	currentDealBatch = dealUpdates.get(dealUpdates.size() - 1);
	if(currentDealBatch.size() == 100)
	{
		currentDealBatch = List();
	}
	else
	{
		dealUpdates.remove(dealUpdates.size() - 1);
	}
	currentDealBatch.add(thisDeal);
	dealUpdates.add(currentDealBatch);
}
//
for each deal in dealUpdates
{
	thisDealCreate = zoho.crm.bulkUpdate("Deals",deal);
	for each  result in thisDealCreate
	{
		if(result.get("code") == "SUCCESS")
		{
			successCount = successCount + 1;
		}
	}
}
//
info "Of " + totalDeals + " Deals, " + successCount + " Deals were successfully updated.";
}
